pragma ComponentBehavior: Bound

import QtQuick
import Caelestia
import Caelestia.Config
import qs.components
import qs.components.controls
import qs.services
import qs.modules.launcher.services

Item {
    id: root

    required property ScreenState screenState
    required property var panels
    required property real maxHeight

    readonly property int padding: Tokens.padding.large
    readonly property int rounding: Tokens.rounding.extraLarge

    implicitWidth: listWrapper.width + padding * 2
    implicitHeight: search.height + listWrapper.height + padding + search.anchors.bottomMargin

    Item {
        id: listWrapper

        implicitWidth: list.width
        implicitHeight: list.height + root.padding

        anchors.horizontalCenter: parent.horizontalCenter
        anchors.bottom: search.top
        anchors.bottomMargin: root.padding

        ContentList {
            id: list

            content: root
            screenState: root.screenState
            panels: root.panels
            maxHeight: root.maxHeight - search.implicitHeight - root.padding * 3
            search: search
            padding: root.padding
            rounding: root.rounding
        }
    }

    SearchBar {
        id: search

        objectName: "launcherSearch"

        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        anchors.margins: root.padding
        anchors.bottomMargin: CUtils.clamp(root.padding - Config.border.thickness, 0, root.padding)

        topPadding: Math.round((Tokens.padding.medium + Tokens.padding.large) / 2)
        bottomPadding: Math.round((Tokens.padding.medium + Tokens.padding.large) / 2)

        placeholderText: qsTr("Type \"%1\" for commands, \"%2\" for clipboard, \"%3\" for emoji")
            .arg(GlobalConfig.launcher.actionPrefix)
            .arg(GlobalConfig.launcher.clipboardPrefix)
            .arg(GlobalConfig.launcher.emojiPrefix)

        onAccepted: {
            const currentItem = list.currentList?.currentItem;
            if (list.showWallpapers) {
                if (currentItem) {
                    if (Colours.scheme === "dynamic" && currentItem.modelData.path !== Wallpapers.actualCurrent)
                        Wallpapers.previewColourLock = true;
                    Wallpapers.setWallpaper(currentItem.modelData.path);
                    root.screenState.launcher = false;
                }
            } else if (list.showEmojis) {
                if (list.currentList?.acceptCurrent)
                    list.currentList.acceptCurrent();
                else if (currentItem?.modelData)
                    Emoji.pasteEmoji(currentItem.modelData.glyph);
                root.screenState.launcher = false;
            } else if (text.startsWith(GlobalConfig.launcher.clipboardPrefix)) {
                if (currentItem?.modelData)
                    Clipboard.selectItem(currentItem.modelData);
                root.screenState.launcher = false;
            } else if (text.startsWith(GlobalConfig.launcher.actionPrefix)) {
                if (currentItem) {
                    if (text.startsWith(`${GlobalConfig.launcher.actionPrefix}calc `))
                        currentItem.onClicked();
                    else
                        currentItem.modelData.onClicked(list.currentList);
                }
            } else if (currentItem?.modelData) {
                Apps.launch(currentItem.modelData);
                root.screenState.launcher = false;
            }
        }

        Keys.onUpPressed: list.moveUp()
        Keys.onDownPressed: list.moveDown()
        Keys.onLeftPressed: event => {
            if (list.showEmojis) {
                list.moveLeft();
                event.accepted = true;
            }
        }
        Keys.onRightPressed: event => {
            if (list.showEmojis) {
                list.moveRight();
                event.accepted = true;
            }
        }

        Keys.onEscapePressed: root.screenState.launcher = false

        Keys.onPressed: event => {
            if (event.key === Qt.Key_Delete && search.text.startsWith(GlobalConfig.launcher.clipboardPrefix)) {
                if (event.modifiers & Qt.ShiftModifier) {
                    Clipboard.wipeAll();
                } else {
                    const currentItem = list.currentList?.currentItem;
                    if (currentItem?.modelData)
                        Clipboard.deleteItem(currentItem.modelData);
                }
                event.accepted = true;
                return;
            }

            if (!GlobalConfig.launcher.vimKeybinds)
                return;

            if (event.modifiers & Qt.ControlModifier) {
                if (event.key === Qt.Key_J || event.key === Qt.Key_N) {
                    list.moveDown();
                    event.accepted = true;
                } else if (event.key === Qt.Key_K || event.key === Qt.Key_P) {
                    list.moveUp();
                    event.accepted = true;
                } else if (event.key === Qt.Key_H && list.showEmojis) {
                    list.moveLeft();
                    event.accepted = true;
                } else if (event.key === Qt.Key_L && list.showEmojis) {
                    list.moveRight();
                    event.accepted = true;
                }
            } else if (event.key === Qt.Key_Tab) {
                list.moveRight();
                event.accepted = true;
            } else if (event.key === Qt.Key_Backtab || (event.key === Qt.Key_Tab && (event.modifiers & Qt.ShiftModifier))) {
                list.moveLeft();
                event.accepted = true;
            }
        }

        Component.onCompleted: forceActiveFocus()

        Connections {
            function onLauncherChanged(): void {
                if (!root.screenState.launcher)
                    search.text = "";
            }

            function onSessionChanged(): void {
                if (!root.screenState.session)
                    search.forceActiveFocus();
            }

            target: root.screenState
        }
    }
}
